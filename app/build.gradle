apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

//输出jar/aar包的版本

//输出jar/aar包的配置
//[baseName]-[appendix]-[version]-[classifier].[extension]
def libVersion = "1.0.6"
def jarFileBaseName = "kangaroo"
def jarType = "debug"

publish {
    userOrg = 'kivensolo'
    groupId = 'com.github.zeke'
    artifactId = 'kangaroo'
    publishVersion = libVersion
    desc = 'Agile Development Tool Libraries'
    website = 'https://github.com/kivensolo/AKangaroo'
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'androidx.recyclerview:recyclerview:1.1.0'
    compileOnly 'androidx.appcompat:appcompat:1.1.0'
    compileOnly "com.github.bumptech.glide:glide:4.5.0"
}

//[baseName]-[appendix]-[version]-[classifier].[extension]
task makeJar(type: Jar){
//    baseName = "Akangaroo"
//    appendix = "demo"
//    version = libVersion
//    classifier = "debug"
//    extension = "jar"
    archiveBaseName = "Akangaroo"
    archiveAppendix = "demo"
    archiveVersion = libVersion
    archiveClassifier = "debug"
    archiveExtension = "jar"
    from('build/intermediates/classes/debug')
    exclude('**/R.class')
    //去掉R$开头的文件
    exclude('**/R\$*.class')
}

task clearJar(type: Delete) {
    // 删除libs目录数据
    //delete 'build/libs/'
    // 有选择性的删除
    FileTree tree = fileTree("build/libs/")
    tree.visit {
        element ->
            if (element.getName().endsWith(".jar") &&
                    !element.getName().contains("javadoc") &&
                    !element.getName().contains("sources")) {
                println "$element.file.name" + " " + element.getName()
                delete element.file
            }
    }
}

// 设置打包任务的分组
def jarGroup = "jars"
makeJar.setGroup(jarGroup)
clearJar.setGroup(jarGroup)
makeJar.dependsOn(clearJar, build)

task sourcesJar(type: Jar) {
    archiveBaseName = jarFileBaseName
//    appendix = 'kangaroo'
    archiveVersion = libVersion
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    version = libVersion
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = jarFileBaseName
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
sourcesJar.setGroup(jarGroup)
javadocJar.setGroup(jarGroup)

artifacts {
    archives sourcesJar
    archives javadocJar
}





